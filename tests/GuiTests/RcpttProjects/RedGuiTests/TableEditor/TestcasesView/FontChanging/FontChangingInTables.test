--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _ceBZ8CakEeesE7mkFuId1w,_b5LYgCm6Eeeb2bJ_EEj3YA
Element-Name: FontChangingInTables
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _Vp4pICajEeesE7mkFuId1w
Runtime-Version: 2.2.0.201706152316
Save-Time: 12/1/17 10:33 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "check-font-for-table" [val table_type] [val font_size "9.0"] [val font_name "Segoe UI"]{
	with [get-editor "TestFile.robot" | get-canvas -after [get-label $table_type] -index 1] {
	    get-property "getFont().getFontData().FontData[0].height" | equals $font_size | verify-true
	    get-property "getFont().getFontData().FontData[0].getName()" | equals $font_name | verify-true
	}
}

proc "check-font-for-section" [val section] [val font_size "9.0"] [val font_name "Segoe UI"]{
	with [get-editor "TestFile.robot" | get-section $section | get-canvas] {
	    get-property "getFont().getFontData().FontData[0].height" | equals $font_size| verify-true
	    get-property "getFont().getFontData().FontData[0].getName()" | equals $font_name | verify-true
	}
}

proc "check-fonts-in-all-tables" [val font_size] [val font_name]{
	//check font in TC
	get-editor "TestFile.robot" | get-tab-folder | get-tab-item "Test Cases" | click
	check-font-for-table -table_type "Test Cases" -font_size $font_size -font_name $font_name

	//check font in KW	
	get-editor "TestFile.robot" | get-tab-folder | get-tab-item Keywords | click
	check-font-for-table -table_type "User Keywords" -font_size $font_size -font_name $font_name

	//check font in Settings	
	get-editor "TestFile.robot" | get-tab-folder | get-tab-item Settings | click
	check-font-for-section -section "General" -font_size $font_size -font_name $font_name
	
	get-editor "TestFile.robot" | get-section Metadata | click
	check-font-for-section -section "Metadata" -font_size $font_size -font_name $font_name
	
	with [get-editor "TestFile.robot"] {
	    get-section Metadata | click
	    get-section Imports | click
	}
	check-font-for-section -section "Imports" -font_size $font_size -font_name $font_name
	get-editor "TestFile.robot" | get-section Imports | click
	
	//check font in Variables	
	get-editor "TestFile.robot" | get-tab-folder | get-tab-item Variables | click
	check-font-for-table -table_type "Variables" -font_size $font_size -font_name $font_name
	
	//check in Source
	get-editor "TestFile.robot" | get-tab-folder | get-tab-item Source | click
	
	with [get-editor "TestFile.robot" | get-text-viewer] {
	    get-property "getFont().getFontData().FontData[0].height" | equals [get-by-os -win "9.75" -linux "10.0"] | verify-true
	    get-property "getFont().getFontData().FontData[0].getName()" | equals [get-by-os -win "(Consolas|Courier New)" -linux "Monospace"] | verify-true
	}
}

//open file
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click


//check in all tables if font was changed
check-fonts-in-all-tables -font_size "15.75" -font_name "Courier New"


//add new kw in TC
get-editor "TestFile.robot" | get-tab-folder | get-tab-item "Test Cases" | click
get-view Outline | get-tree | select "TestFile.robot/Test Cases/test 1"
with [get-editor "TestFile.robot"] {
    with [get-canvas -after [get-label "Test Cases"] -index 1] {
        key-type Down -times 2
        key-type Enter
    }
    with [get-editbox -after [get-label "Test Cases"] -index 1] {
        type-text "New kw"
        key-type Enter
    }
    get-tab-folder | get-tab-item Keywords | click
}

//add new KW
get-view Outline | get-tree | select "TestFile.robot/Keywords/new"
with [get-editor "TestFile.robot"] {
    with [get-canvas -after [get-label "User Keywords"] -index 1] {
        key-type Down -times 5
        key-type Enter
    }
}

get-editor "TestFile.robot" | get-tab-folder | get-tab-item "Variables" | click

get-view Outline | get-tree | select "TestFile.robot/Variables/newDict"
with [get-editor "TestFile.robot"] {
    with [get-canvas -after [get-label Variables] -index 1] {
        key-type Left
        key-type Right
        key-type Enter
    }
    //change dict name
    with [get-editbox -after [get-label Variables] -index 1] {
        type-text "v1"
        key-type Enter
    }
    get-editbox -after [get-label Variables] -index 1 | key-type  "TRAVERSE_TAB_NEXT"
}

//check that font is the same
check-fonts-in-all-tables -font_size "15.75" -font_name "Courier New"


//reset font
get-preferences-menu | click
with [get-window Preferences] {
    get-tree | select "General/Appearance/Colors and Fonts"
    get-tree -after [get-label "Colors and Fonts (? = any character, * = any string):"] 
        | select "Robot/Table Font"
    get-button Reset | click
    get-button Apply | click
    get-button $Apply_and_OK_in_preference | click
}

get-editor "TestFile.robot" | get-text-viewer | set-caret-pos 16 8
get-button "Save (M1+S)" | click
get-view "Project Explorer" | click
get-editor "TestFile.robot" | close
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click


check-fonts-in-all-tables -font_size [get-by-os -win "[89]\.\d\d?"  -linux "11.0"] -font_name [get-by-os -win "(Segoe UI|Tahoma)" -linux "Cantarell"]

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
