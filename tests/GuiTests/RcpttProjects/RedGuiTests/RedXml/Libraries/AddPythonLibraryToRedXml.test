--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _rNY3QJKkEeWGA6GsgqW2Mw
Element-Name: AddPythonLibraryToRedXml
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _RTKmUJNnEeWXp4iZQnTw0A
Runtime-Version: 2.2.0.201706152316
Save-Time: 2/14/18 4:07 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-view "Project Explorer" | get-tree | select "ProjectWithResource/TestSuite.robot" | double-click

// add to Source path to library Calculator 
with [get-editor "TestSuite.robot" | get-text-viewer] {
    set-caret-pos 8 1
    type-text "Library	external_lib/CalculatorLibrary.py "

}

// add Keyword from Calc lib, now RED does not see it - should be a problem reporteed
with [get-editor "TestSuite.robot"] {
    with [get-text-viewer] {
        set-caret-pos 3 19
        key-type Enter
        type-text "Push button  text"
        key-type "M1+s"
    }
}
wait 2000
// check if lib and KW is reported as unknown
with [get-view Problems | get-tree] {
    get-item "Errors \\(2 items\\)" | get-property "getItemCount()" | equals 2 | verify-true
    get-item "Errors \\(2 items\\)" | get-property "getItems().TreeItem[0].getText()" 
        | equals "Unknown 'external_lib/CalculatorLibrary.py' library. Try to use Quick Fix (Ctrl+1) or add library to red.xml for proper validation" | verify-true
    get-item "Errors \\(2 items\\)" | get-property "getItems().TreeItem[1].getText()" 
        | equals "Unknown keyword 'Push button'" | verify-true
}

// open red.xml and add CalculatorLibrary to Python libs by button
get-view "Project Explorer" | get-tree | select "ProjectWithResource/red.xml" | double-click

wait-for-loading-red-xml "ProjectWithResource/red.xml" 
with [get-editor "ProjectWithResource/red.xml" | get-tab-folder] {
    get-property "pages[0]" | equals General | verify-true
    get-property "pages[1]" | equals "Referenced libraries" | verify-true
    get-property "pages[2]" | equals "Variable files" | verify-true
    get-property "pages[3]" | equals Validation | verify-true
    get-property activePage | equals General | verify-true
    get-property tabCount | equals 4 | verify-true
}
get-editor "ProjectWithResource/red.xml" | get-tab-folder | get-tab-item "Referenced libraries" | click
set-dialog-result File "platform:/resource/ProjectWithResource/external_lib/CalculatorLibrary.py"
get-editor "ProjectWithResource/red.xml" | get-section "Referenced libraries" | get-button "Add Python library" | click
get-editor "ProjectWithResource/red.xml" | get-section "Referenced libraries" | get-button "Add Python library" 
    | key-type "M1+s"

// go back to Source and check errors - there should be zero
get-tab-folder -index 1 | get-tab-item "TestSuite.robot" | click
with [get-editor "TestSuite.robot" | get-text-viewer] {
    set-caret-pos 11 27
    key-type "M1+s"
}

get-menu "Project/Clean..." | click
get-window Clean | get-button $OK_Clean_button | click

wait-for-zero-items -times 5 -delay 1000

// check if libspec is generated from CalculatorLibrary
with [get-view "Project Explorer" | get-tree] {
    get-item "ProjectWithResource/Robot Referenced libraries/CalculatorLibrary ProjectWithResource\\/external_lib \\(4\\)" 
        | get-property caption | equals "CalculatorLibrary ProjectWithResource/external_lib (4)" | verify-true
    get-item "ProjectWithResource/Robot Referenced libraries/CalculatorLibrary ProjectWithResource\\/external_lib \\(4\\)" 
        | get-property "getItems().TreeItem[0].getText()" | equals "Push Button" | verify-true
    get-item "ProjectWithResource/Robot Referenced libraries/CalculatorLibrary ProjectWithResource\\/external_lib \\(4\\)" 
        | get-property "getItems().TreeItem[1].getText()" | equals "Push Buttons" | verify-true
    get-item "ProjectWithResource/Robot Referenced libraries/CalculatorLibrary ProjectWithResource\\/external_lib \\(4\\)" 
        | get-property "getItems().TreeItem[2].getText()" | equals "Result Should Be" | verify-true
    get-item "ProjectWithResource/Robot Referenced libraries/CalculatorLibrary ProjectWithResource\\/external_lib \\(4\\)" 
        | get-property "getItems().TreeItem[3].getText()" | equals "Should Cause Error" | verify-true
}


// remove lib entry from red.xml
get-view "Project Explorer" | get-tree | select "ProjectWithResource/red.xml" | double-click
with [get-editor "ProjectWithResource/red.xml"] {
    get-tab-folder | get-tab-item "Referenced libraries" | click
    with [get-section "Referenced libraries" | get-table] {
        select "CalculatorLibrary - ProjectWithResource\\/external_lib" | key-type Del
        key-type "M1+s"
    }
    close
}
// use Quick fix to discover lib and add it to red.xml
with [get-editor "TestSuite.robot" | get-text-viewer] {
    type-text " "
    wait 2000
    set-caret-pos 9 38
    hover-text 9 28
    get-menu "Quick Fix" | click
    wait 500
}

// discovery
get-window -from "CompletionProposalPopup.createProposalSelector()" | get-table 
    | select "Discover 'external_lib\\/CalculatorLibrary.py' and add to configuration" | click -default

get-editor "ProjectWithResource/red.xml" | key-type "M1+s"
// revalidate
with [get-editor "TestSuite.robot" | get-text-viewer] {
    set-caret-pos 8 40
    type-text " "
}

wait-for-zero-items -times 4 -delay 1000
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
