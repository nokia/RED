--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _d8aMACuvEeW667Afdo4DcA,_OCxtEBRGEeWWV9IuysVI_A,_2Q6oEPOSEeaWpbE3jg39cA,_jcbUQGu1Eeeps9cepQRw2Q
Element-Name: HitCountAndConditionalInDebug
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _4qmyUPRNEeaWpbE3jg39cA
Runtime-Version: 2.2.0.201706152316
Save-Time: 10/20/17 5:23 PM
Tags: Debug
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//set test
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click
get-editor "TestFile.robot" | get-text-viewer | set-text "*** Variables ***\n"
    + "@{NUMBERS}      ${1}    ${2}    ${5}  \n"
    + "*** Test Cases ***\n"
    + "Continue Example\n"
    + "    my keyword\n"
    + "    my keyword\n"
    + "    my keyword\n"
    + "    \n"
    + "    :FOR    ${x}   IN RANGE   1   15\n"
    + "    \\    second kw\n"
    + "    \\    log  ${x}\n"
    + "*** Keywords ***\n\n"
    + "my keyword\n"
    + "    log  8\n"
    + "    [Return]    True\n"
    + "    \n"
    + "second kw\n"
    + "    Should Be Equal    1    1" 

get-button "Save \((Ctrl|M1)\+S\)" | click
    
//add BP
with [get-editor "TestFile.robot" | get-left-ruler | get-ruler-column AnnotationColumn] {
    double-click-ruler 19
}

//set hit counts 15 and conditional
get-button "Open Perspective" | click
get-window "Open Perspective" | get-table | select Debug | double-click
get-view Breakpoints | get-tree | select "TestFile.robot [line: 19]"

with [get-view Breakpoints] {
    get-button "Hit count:" | check
    get-editbox | set-text 5
    get-button Conditional | check
}

with [get-view Breakpoints] {
    get-combo | set-text  "Should be equal  2  2"
    get-tree | key-type "M1+s"
}    

get-editor "TestFile.robot" | get-text-viewer | set-caret-pos 8 5

exec-with-options -disableJobWaiting -command {
	get-button "Debug.*" | click -arrow
	get-menu "Debug As/1 Robot Test" | click
	wait-for-thread -file "TestFile.robot" -times 15
	
	check-debug-marker-on-ruler -line 19 -script_name "TestFile.robot"
	
	get-view "Message Log" | get-editbox | get-property text 
	    | contains "INFO : 4" | verify-true
	get-button "Resume (F8)" | click
}
wait-for-console -times 10 -delay 1000
	
//add incorrect conditional
with [get-view Breakpoints] {
    get-combo | set-text "Should be equal  3"
    get-tree | key-type "M1+s"
}    

get-editor "TestFile.robot" | get-text-viewer | set-caret-pos 8 5

exec-with-options -disableJobWaiting -command {
	get-button "Debug.*" | click -arrow
	get-menu "Debug As/1 Robot Test" | click

	try -times 20 -delay 1000 -command{
		get-window "Debugger Error" 
		    | get-label "Error occurred when evaluating breakpoint condition. Keyword 'BuiltIn.Should Be Equal' expected 2 to 5 arguments, got 1." | get-property caption 
		    | equals "Error occurred when evaluating breakpoint condition. Keyword 'BuiltIn.Should Be Equal' expected 2 to 5 arguments, got 1." | verify-true
	}    
	get-window "Debugger Error" | get-button OK | click
	
	get-button "Resume (F8)" | click
}
wait-for-console -times 10 -delay 1000

//set count that will be never true
with [get-view Breakpoints | get-editbox] {
    click-text 1
    set-text 99
    key-type "M1+s"
}

//run test - test will be not stopped
get-editor "TestFile.robot" | get-text-viewer | set-caret-pos 12 17
get-button "Debug.*" | click -arrow
get-menu "Debug As/1 Robot Test" | click

wait-for-console -times 10 -delay 1000
get-view "Message Log" | get-editbox | get-property text 
    | contains "Ending test: RobotProject.TestFile.Continue Example" | verify-true
	
	
	
	
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
