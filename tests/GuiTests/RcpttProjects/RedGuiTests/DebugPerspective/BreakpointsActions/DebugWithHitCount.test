--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _2Q6oEPOSEeaWpbE3jg39cA,_d8aMACuvEeW667Afdo4DcA,_OCxtEBRGEeWWV9IuysVI_A,_jcbUQGu1Eeeps9cepQRw2Q
Element-Name: DebugWithHitCount
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _R0nm0PKlEeadkqPlInmy5w
Runtime-Version: 2.2.0.201706152316
Save-Time: 11/6/17 4:06 PM
Tags: Debug
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click
get-editor "TestFile.robot" | get-text-viewer | set-text "*** Variables ***\n"
    + "@{NUMBERS}      ${1}    ${2}    ${5}  \n"
    + "*** Test Cases ***\n"
    + "Continue Example\n"
    + "    my keyword\n"
    + "    my keyword\n"
    + "    my keyword\n"
    + "    \n"
    + "    :FOR    ${x}   IN RANGE   1   12\n"
    + "    \\    second kw\n"
    + "    \\    log  ${x}\n"
    + "*** Keywords ***\n\n"
    + "my keyword\n"
    + "    log  8\n"
    + "    [Return]    True\n"
    + "    \n"
    + "second kw\n"
    + "    Should Be Equal    1    1" 

get-button "Save \((Ctrl|M1)\+S\)" | click
    
//set 2 BP in KW     
with [get-editor "TestFile.robot" | get-left-ruler | get-ruler-column AnnotationColumn] {
    double-click-ruler 15
    double-click-ruler 19
}
//open Debug perspective to set HIT counters
get-button "Open Perspective" | click
get-window "Open Perspective" | get-table | select Debug | double-click

//set value 3
with [get-view Breakpoints] {
    get-tree | select "TestFile.robot [line: 15]"
    get-button "Hit count:" | check
    get-editbox | set-text 3
}
get-button "Save (M1+S)" | click

//for line 19 set "6"
with [get-view Breakpoints] {
    get-tree | select "TestFile.robot [line: 19]"
    get-button "Hit count:" | check
    get-editbox | set-text 6
}
get-button "Save (M1+S)" | click

//go to Robot perspective and run test
get-button Robot | click
exec-with-options -disableJobWaiting -command {
	get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | get-menu "Debug As/1 Robot Test" | click
	wait-for-thread  -file "TestFile.robot"
	
	//check that 2 previouse "8" were printed
	try -times 4 -delay 500 -command{
		get-view "Message Log" | get-editbox | get-property text 
		    | equals "Starting test: RobotProject.TestFile.Continue Example\n"
		    + ".* : INFO : 8\n"
		    + ".* : INFO : 8" | verify-true
	}    
	check-debug-marker-on-ruler -line 15 -script_name "TestFile.robot"
	
	//resume - test should be stopped when value will be 15
	get-button "Resume (F8)" | click
	wait-for-specified_line-in-first-thread -line 19 -nr_of_frames 6
	check-debug-marker-on-ruler -line 19 -script_name "TestFile.robot"
	
	//check if 5 is in message log
	get-view "Message Log" | get-editbox | get-property "getLineCount()" | equals 10 | verify-true
	get-view "Message Log" | get-editbox | get-property text 
	    | contains " : INFO : 5" | verify-true
	    
	//set now value "2" durint test (now new value is set, counter is not addded)
	with [get-view Breakpoints | get-editbox] {
	    set-text 2
	    key-type "M1+s"
	}
	//reume test - test should be stopped on 7 hit
	get-button "Resume (F8)" | click
	wait-for-thread -file "TestFile.robot" -times 5
	check-debug-marker-on-ruler -line 19 -script_name "TestFile.robot"

	with [get-view "Message Log" | get-editbox] {
	    get-property "getLineCount()" | equals 12 | verify-true
	    get-property text |contains ": INFO : 7\n" | verify-true
	}
	//resume to end test
	get-button "Resume (F8)" | click
}	

wait-for-console -times 10 -delay 1000
 
//uncheck hit count for line 19	
get-view Breakpoints | get-button "Hit count:" | uncheck
get-button "Save (M1+S)" | click

//check that value is disabled but visible 
get-view Breakpoints | get-editbox | get-property text | equals "2" | verify-true
get-view Breakpoints | get-editbox | get-property "isEnabled()" | equals false | verify-true


exec-with-options -disableJobWaiting -command {
	//debug test
	get-editor "TestFile.robot" | get-text-viewer | set-caret-pos 9 37
	get-button "Debug.*" | click
	wait-for-thread -file "TestFile.robot"
	
	check-debug-marker-on-ruler -line 15 -script_name "TestFile.robot"
	
	//resume - test will be stopped on second kw
	get-button "Resume (F8)" | click
	wait-for-specified_line-in-first-thread -line 19 -nr_of_frames 6
	check-debug-marker-on-ruler -line 19 -script_name "TestFile.robot"
	
	get-view "Message Log" | get-editbox | get-property text 
	    | equals "Starting test: RobotProject.TestFile.Continue Example\n"
	    + ".* : INFO : 8\n"
	    + ".* : INFO : 8\n"
	    + ".* : INFO : 8" | verify-true
	    
	get-button "Resume (F8)" | click
	wait 500
	//new print will be added - no skipp to 36 line
	get-view "Message Log" | get-editbox | get-property text 
	    | equals "Starting test: RobotProject.TestFile.Continue Example\n"
	    + ".* : INFO : 8\n"
	    + ".* : INFO : 8\n"
	    + ".* : INFO : 8\n"
	    + ".* : INFO : 1" | verify-true
	get-button "Terminate (M1+F2)" | click
}

//set fosuc on BP where hit count is declare
get-view Breakpoints | get-tree | select "TestFile.robot [line: 15] [hit count: 3]"

//remove all BP
get-view Breakpoints | get-button "Remove All Breakpoints" | click
get-window "Remove All Breakpoints" | get-button Yes | click	

//check that no value was left in text box
with [get-view Breakpoints] {
    get-button "Hit count:" | get-property selected | equals false | verify-true  
    get-editbox | get-property text | equals "" | verify-true	
}

//set B in test cases on second occurance of kw 
with [get-editor "TestFile.robot" | get-left-ruler | get-ruler-column AnnotationColumn] {
    double-click-ruler 7
}

//set value = 2
with [get-view Breakpoints] {
    get-button "Hit count:" | check
    with [get-editbox] {
        set-text 2
        key-type "M1+s"
        set-text 2
    }
}

//run test
get-editor "TestFile.robot" | get-text-viewer | set-caret-pos 4 17

get-button "Debug.*" | click

wait-for-console -times 10 -delay 1000
//check that test was not stopped
get-view "Message Log" | get-editbox | get-property text 
    | contains  "Ending test: RobotProject.TestFile.Continue Example" | verify-true
    

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
