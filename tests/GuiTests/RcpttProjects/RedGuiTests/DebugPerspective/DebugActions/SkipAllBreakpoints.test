--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _OCxtEBRGEeWWV9IuysVI_A,_NvY3ABRGEeWWV9IuysVI_A,_d8aMACuvEeW667Afdo4DcA,_2Q6oEPOSEeaWpbE3jg39cA
Element-Name: SkipAllBreakpoints
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _OdaRMJbhEeaa4JtzzKMFzw
Runtime-Version: 2.2.0.201706152316
Save-Time: 10/20/17 5:24 PM
Tags: Debug
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "skip-all-bp-and-check-selection" [val is_selected true]{
	get-button "Skip All Breakpoints (M1+M3+B)"  | click
	get-button "Skip All Breakpoints (M1+M3+B)" | get-property selected| equals $is_selected | verify-true
}
	
//open Debug perspective and switch on skipping all BP
get-button "Open Perspective" | click
get-window "Open Perspective" | get-table | select Debug | double-click
wait 1000


skip-all-bp-and-check-selection 

//go to test and add some BP
get-button Robot | click
get-view "Project Explorer" | get-tree | select "ScriptWithResource/BigScript.robot" | double-click
wait 1000
with [get-editor "BigScript.robot" | get-left-ruler | get-ruler-column AnnotationColumn] {
    double-click-ruler 17
    double-click-ruler 23
    double-click-ruler 27
    double-click-ruler 37
    double-click-ruler 50
    double-click-ruler 67
    double-click-ruler 92
}

//run test
get-view "Project Explorer" | get-tree | select "ScriptWithResource/BigScript.robot" | get-menu "Debug As/1 Robot Test" 
    | click

wait-for-console -times 10 -delay 1000

//check if test ended with no BP
get-view Debug | get-tree 
    | get-item "<terminated>BigScript.robot \\[Robot\\]/<terminated, exit value.*" 
    | get-property caption | equals "<terminated, exit value: .*" | verify-true
    
//turn off skipping        
skip-all-bp-and-check-selection -is_selected false

//go to test and run it
get-button Robot | click

exec-with-options -disableJobWaiting -command {
	get-view "Project Explorer" | get-tree | select "ScriptWithResource/BigScript.robot" | get-menu "Debug As/1 Robot Test" 
    	| click
	wait 3000
	wait-for-thread -file "BigScript.robot"
	
	//check if not test was stopped
	check-debug-marker-on-ruler -line 17 -script_name "BigScript.robot"
	get-button "Terminate (M1+F2)" | click

}

//remove all BP
with [get-view Breakpoints] {
    click
    get-button "Remove All Breakpoints" | click
}
get-window "Remove All Breakpoints" | get-button Yes | click

//add new 2 BP
with [get-editor "BigScript.robot" | get-left-ruler | get-ruler-column AnnotationColumn] {
    double-click-ruler 22
    double-click-ruler 29
}

//now skipp all BP
skip-all-bp-and-check-selection 

//clear console
with [get-view Console] {
    click
    get-button "Clear Console" | click
}

///set focus on editor to have focus and could run test
get-editor "BigScript.robot" | get-text-viewer | set-caret-pos 37 1
get-button "Debug.*" | click

wait-for-console -times 10 -delay 1000

//check if test ended with no BP
get-view Console | get-editbox -after [get-label "<terminated> .*"] 
    | get-property text -raw | not-eq "" | verify-true
    
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
