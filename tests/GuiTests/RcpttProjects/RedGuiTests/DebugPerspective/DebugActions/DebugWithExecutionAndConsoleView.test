--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _OCxtEBRGEeWWV9IuysVI_A,_znjMEBtDEeWSh6BKjAxDIw
Element-Name: DebugWithExecutionAndConsoleView
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _OVxNoN8BEeWvV6AMcH1t9Q
Runtime-Version: 2.1.0.201606221726
Save-Time: 4/28/17 4:21 PM
Tags: Debug
Testcase-Type: ecl
Verifications: _ytMH0N-fEeWvV6AMcH1t9Q

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | double-click

//set simple test and save it
get-editor "TestFile.robot" | get-text-viewer  | set-text "*** Test Cases ***\n"
    + "test\n"
    + "    log  56s\n"
    + "    \n"
    + "test 2\n"
    + "    log  88   \n\n"
    + "test 3\n"
    + "    log  100"
    
with [get-editor "TestFile.robot" | get-text-viewer] {
    key-type "M1+s"
}    
//set breakpoint
get-editor "TestFile.robot" | get-left-ruler | get-ruler-column AnnotationColumn | double-click-ruler 6

//run test
exec-with-options -disableJobWaiting -command {
	get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | get-menu "Debug As/1 Robot Test" | click
	wait 3000
	try -times 5 -delay 1000 -command{
		get-view Debug | get-tree 
		    | get-item "TestFile.robot \\[Robot\\]/Robot Test at 127.0.0.1:\d{1,5}/Thread \\[main\\]/BuiltIn.Log \\[line:6\\]" 
		    | get-property caption | equals "BuiltIn.Log [line:6]" | verify-true
	}
	
	with [get-view "Execution View" | get-tree] {
	    get-item "RobotProject/TestFile" | get-property childCount | equals 3 | verify-true
	    get-item "RobotProject/TestFile" | get-property "getItems().TreeItem[0].getText()" | equals "test \(.* s\)" 
	        | verify-true
	    get-item "RobotProject/TestFile" | get-property "getItems().TreeItem[1].getText()" | equals "test 2" | verify-true
	    get-item "RobotProject/TestFile" | get-property "getItems().TreeItem[2].getText()" | equals "test 3" | verify-true
	    
	    // test ok
	    get-item "RobotProject/TestFile/test \\(.* s\\)" | get-property "image.path" 
	        | equals "org.robotframework.ide.eclipse.main.plugin/resources/testok.png" | verify-true
	    
	    //test is running
	    get-item "RobotProject/TestFile/test 2" | get-property "image.path" 
	        | equals "org.robotframework.ide.eclipse.main.plugin/resources/testrun.png" | verify-true
	        
	    //test that will be run    
	    get-item "RobotProject/TestFile/test 3" | get-property "image.path" 
	        | equals "org.robotframework.ide.eclipse.main.plugin/resources/test.png" | verify-true
	}
	with [get-view "Execution View"] {
	    get-canvas | get-property "getProgress()" | equals "0.3333333333333333" | verify-true
	    get-canvas | get-property "getBarColor()" | equals "#00B400" | verify-true
	    get-label "Tests: 2/3" | get-property caption | equals "Tests: 2/3" | verify-true
	    get-label "Passed: 1" | get-property caption | equals "Passed: 1" | verify-true
	    get-label "Failed: 0" | get-property caption | equals "Failed: 0" | verify-true  
	}
	 
	//change focus
	get-view "Execution View" | close
    wait 1000
    get-button "Resume (F8)" | click
    wait 1000
    
  
}
wait-for-console
get-view Console | get-text-viewer | get-property text 
    | equals "Command: .* -m robot.run --listener .*TestRunnerAgent.py:\d{1,6} --argumentfile .*\.arg .*RobotProject\n"
    + "Suite Executor: Robot Framework .*\n"
    + "==============================================================================\n"
    + "RobotProject                                                                  \n"
    + "==============================================================================\n"
    + "RobotProject.TestFile                                                         \n"
    + "==============================================================================\n"
    + "test                                                                  \| PASS \|\n"
    + "------------------------------------------------------------------------------\n"
    + "test 2                                                                \| PASS \|\n"
    + "------------------------------------------------------------------------------\n"
    + "test 3                                                                \| PASS \|\n"
    + "------------------------------------------------------------------------------\n"
    + "RobotProject.TestFile                                                 \| PASS \|\n"
    + "3 critical tests, 3 passed, 0 failed\n"
    + "3 tests total, 3 passed, 0 failed\n"
    + "==============================================================================\n"
    + "RobotProject                                                          \| PASS \|\n"
    + "3 critical tests, 3 passed, 0 failed\n"
    + "3 tests total, 3 passed, 0 failed\n"
    + "==============================================================================\n"
    + "Output: .*output.xml\n"
    + "Log:     .*log.html(\nReport:  .*report.html)?" | verify-true

get-menu "Window/Show View/Execution View" | click

with [get-view "Execution View" | get-tree] {
    get-item "RobotProject \\(.* s\\)/TestFile \\(.* s\\)" | expand
}  
    
//check labels
with [get-view "Execution View"] {
    get-canvas | get-property "getProgress()" | equals "1.0" | verify-true
    get-label "Tests: 3/3" | get-property caption | equals "Tests: 3/3" | verify-true
    get-label "Passed: 3" | get-property caption | equals "Passed: 3" | verify-true
    get-label "Failed: 0" | get-property caption | equals "Failed: 0" | verify-true
    get-canvas | get-property "getBarColor()" | equals "#00B400" | verify-true
}

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
