--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _d8aMACuvEeW667Afdo4DcA,_OCxtEBRGEeWWV9IuysVI_A,_2Q6oEPOSEeaWpbE3jg39cA,_pjyZ0Af5Eee0EKWnoBYU9g,_ur0DkAf5Eee0EKWnoBYU9g,_s-ctEA4_EeePcNCRX_abWA
Element-Name: RemoteDebugForTheSameTestNameSuspend
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _TDBEsH2vEeeUiMBqkyjlBQ
Runtime-Version: 2.2.0.201706152316
Save-Time: 10/20/17 4:07 PM
Tags: Debug
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "check-if-red-marker-is-shown" [val text]{
	with [get-editor "TestFile.robot" | get-text-viewer] {
		    get-property "markers['1'][0].Type" | equals "org.robotframework.red.debug.currentErrorIP" | verify-true
		    get-property "markers['1'][0].text" | equals $text| verify-true
	}
}		

proc "check-error-in-editor" [val text]{
	get-editor "TestFile.robot" | get-text-viewer | hover-text 1 10
	get-window -from "Information Window" | get-editbox | get-property text 
	    | equals $text | verify-true
	check-if-red-marker-is-shown -text $text
}


proc "check-decorators-on-frames"  [val error_list] [val suite] [val project]{
	
	$error_list| each [val index]{
		get-view Debug | get-tree |get-item "RemoteProject \\[Robot Remote\\]/<suspended>Remote Robot Test at 127.0.0.1:\d{1,5}/Tests execution thread/.*" -index $index 
			| get-property "image.decorations[0].path" 
			| equals ".*/error.png" | verify-true
	}
	with [get-view Debug | get-tree] {	
	    get-item "RemoteProject \\[Robot Remote\\]/<suspended>Remote Robot Test at 127.0.0.1:\d{1,5}/Tests execution thread/.*" -index $suite 
	    	| get-property "image.decorations[0].path" 
	        | equals ".*/decorator_robot.png" | verify-true
	    get-item "RemoteProject \\[Robot Remote\\]/<suspended>Remote Robot Test at 127.0.0.1:\d{1,5}/Tests execution thread/.*" -index $project 
	    	| get-property "image.decorations[0].path"  
	        | equals ".*decorator_folder.png" | verify-true
	}
}	

proc "check-thread-list"  [val first_items]{
	let [val last_items [list "[Suite] TestFile" "[Suite] RemoteProject"]]{
		let [val all_items [emit [$first_items | invoke getElements] [$last_items | invoke getElements] | to-list]]{
			$all_items | each [val thread] [val index]{
	
				with [get-view Debug | get-tree] {
				    get-item "RemoteProject \\[Robot Remote\\]/<suspended>Remote Robot Test at 127.0.0.1:\d{1,5}/Tests execution thread" 
				        | get-property [format "getItems().TreeItem[%s].getText()" $index] | equals  $thread| verify-true
				}
			
			get-view Debug | get-tree 
				| get-item "RemoteProject \\[Robot Remote\\]/<suspended>Remote Robot Test at 127.0.0.1:43981/Tests execution thread" 
			    	| get-property childCount | equals [$all_items | invoke getElements | invoke size]| verify-true
			}	
		}		        
	}		        
}

//decrease timeout in preference
get-preferences-menu | click
with [get-window Preferences] {
    get-tree | select "Robot Framework/Launching/Default Launch Configurations"
    get-group "Listener tab" | get-editbox -after [get-label "Server connection timeout [s]:"] | set-text 15
    get-button Apply | click
    get-button $Apply_and_OK_in_preference | click
}
//change test content - now there is no test with the same name
get-view "Project Explorer" | get-tree | select "RemoteProject/TestFile.robot" | double-click
get-editor "TestFile.robot" | get-text-viewer | set-text "*** Test Cases ***\n"
    + "my test\n"
    + "    log  8\n"
    + "    Should Be Equal As Integers    4    4 \n"
    + "    \n"
    + "second test\n"
    + "    Catenate    5   6" 

//change name of kw in resource
get-view "Project Explorer" | get-tree | select "RemoteProject/res.robot" | double-click
with [get-editor "res.robot" | get-text-viewer] {
    set-caret-pos 2 12
    type-text 1
    key-type "M1+s"
}
get-editor "TestFile.robot" | click
get-editor "res.robot" | close

//save and BP
get-button "Save \((Ctrl|M1)\+S\)" | click
get-editor "TestFile.robot" | get-left-ruler | get-ruler-column AnnotationColumn | double-click-ruler 7


let [val remote_project_path  [format "%s/Remote" $user_folder] ]{
	//export listener
	export-agent-to-folder -folder $remote_project_path
	
	//run test
	exec-with-options -disableJobWaiting -command {
		get-view "Project Explorer" | get-tree | select "RemoteProject/TestFile.robot" 
		    | get-menu "Debug As/2 Robot Remote Test" | click
		
		wait-for-remote-connection 
		
		run-remote-test-in-background -path $remote_project_path -project_name "RemoteProject"
		
		check-debug-error-for-wrong-state
		get-window "Debugger error" | get-button Suspend | click
		
		//check if errors are shown in red highlightning and marker
		check-error-in-editor "RED debugger: Unable to find test 'test 1'\n"
			    + "There is no such test in current suite\n"
			    + "Unable to find executable call of 'BuiltIn.Log' keyword"

		//check decorators in thread list
		check-decorators-on-frames -error_list [list 0] -suite 1 -project 2
				
		//check thread list		
		check-thread-list [list "[Test] test 1 [line: 1]"]
		
		//-------------------------------------------------------------------------------------------------------------
		//go to kw in test
		get-button "Step Into (F5)" | click
		
		try -times 4 -delay 500 -command{
			get-editor "BuiltIn.Log"
		}
		//Log kw editor is shown
		get-editor "BuiltIn.Log" | get-canvas | get-property text 
		    | equals "The keyword BuiltIn.Log is not a User Keyword but is written in external library. RED debugger alone is not able to debug both Robot and python code however it is possible to setup cooperation with other debugger (PyDev for example).\r\n"
		    + "For more information on setting up Robot and Python debugging sessions please refer to RED user guide in help system.\r\n"
		    + "\r\n"
		    + "Click here to try opening source of this keyword.\r\n"
		    + "\r\n"
		    + "Note: if you don't want to see this page you can configure debugger in Preferences so that it will never suspend inside library keywords when stepping.\r\n" 
		    | verify-true
		 
		//go to Log kw defintion    
		get-editor "BuiltIn.Log" | get-link "Click here" | click
		
		try -times 5 -delay 500 -command{
			get-editor "BuiltIn.py"
		}		
		get-editor "BuiltIn.py" | get-text-viewer | get-property "getSelectionText()" | equals log | verify-true
		get-editor "BuiltIn.Log" | click
		get-editor "BuiltIn.py" | close    
		
		check-decorators-on-frames -error_list [list 1] -suite 2 -project 3
		check-thread-list [list "BuiltIn.Log" "[Test] test 1 [line: 1]"]

		//-------------------------------------------------------------------------------------------------------------
		//go out from log
		get-button "Step Into (F5)" | click
		check-error-in-editor -text "RED debugger: Unable to find test 'test 1'\n"
		    + "There is no such test in current suite\n"
		    + "Unable to find executable call of 'BuiltIn.Log' keyword" 
		
		check-decorators-on-frames -error_list [list 0] -suite 1 -project 2	
		check-thread-list [list "[Test] test 1 [line: 1]"]
		
		
		//-------------------------------------------------------------------------------------------------------------
		//go to next text
		get-button "Step Into (F5)" | click
		try -times 4 -delay 1000 -command{
			get-window "Debugger error" | get-button Suspend | click
		}
		check-error-in-editor  -text "RED debugger: Unable to find test 'test 2'\n"
		    + "There is no such test in current suite\n"
		    + "Unable to find executable call of 'res.kw from res' keyword"
		check-decorators-on-frames -error_list [list 0] -suite 1 -project 2
		check-thread-list [list "[Test] test 2 [line: 1]"]
		
		
		//-------------------------------------------------------------------------------------------------------------
		
		//step in to kw that is in resource file
		get-button "Step Into (F5)" | click
		try -times 3 -delay 1000 -command{
			get-editor "res.kw from res"
		}
		
		if [get-by-os -win Windows -linux "Linux" | equals Windows]{
			get-editor "res.kw from res" | get-editbox -after [get-label "Unable to find keyword 'res.kw from res'"] 
			    | get-property text 
			    | equals [format "There is no such keyword accessible from '%s%s%sRemoteProject%s%sTestFile.robot' file" $RED_workspace_path_double_slaches  $sys_sep $sys_sep $sys_sep $sys_sep]
			    | verify-true
			    
		}-else{
			get-editor "res.kw from res" | get-editbox -after [get-label "Unable to find keyword 'res.kw from res'"] 
			    | get-property text 
			    | equals [format "There is no such keyword accessible from '%s%sRemoteProject%sTestFile.robot' file" $RED_workspace_path $sys_sep $sys_sep]
			    | verify-true
		}  
		
		check-decorators-on-frames -error_list [list 0 1] -suite 2 -project 3
		check-thread-list [list "res.kw from res" "[Test] test 2 [line: 1]"]
		
		//-------------------------------------------------------------------------------------------------------------  
		with [get-button "Step Into (F5)"] {
		    click
		    wait 1000
		    click
		}
		try -times 4 -delay 500 -command{
			get-editor "BuiltIn.Log"
		}
		get-editor "BuiltIn.Log" | get-label "Keyword BuiltIn.Log contained in library" | get-property caption 
   			 | equals "Keyword BuiltIn.Log contained in library" | verify-true
		    
		check-decorators-on-frames -error_list [list 1 2] -suite 3 -project 4
		check-thread-list [list "BuiltIn.Log" "res.kw from res" "[Test] test 2 [line: 1]" ]    
		    
		with [get-button "Step Into (F5)"] {
		    click
		    wait 1000
		    click
		}
		
		check-error-in-editor  -text "RED debugger: Unable to find test 'test 2'\n"
		    + "There is no such test in current suite\n"
		    + "Unable to find executable call of 'res.kw from res' keyword"
		check-decorators-on-frames -error_list [list 0] -suite 1 -project 2
		check-thread-list [list "[Test] test 2 [line: 1]"]
		
		get-button "Step Into (F5)" | click
				
	}		
}	

//test should not be stopped on BP but should be run
get-view "Message Log" | get-editbox | get-property text | equals "Starting test: RemoteProject.TestFile.test 1\n"
    + ".* : INFO : 5\n"
    + "Ending test: RemoteProject.TestFile.test 1\n\n"
    + "Starting test: RemoteProject.TestFile.test 2\n"
    + ".* : INFO : 9\n"
    + "Ending test: RemoteProject.TestFile.test 2" | verify-true




------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
