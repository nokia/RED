--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _d8aMACuvEeW667Afdo4DcA,_OCxtEBRGEeWWV9IuysVI_A,_jcbUQGu1Eeeps9cepQRw2Q,_2Q6oEPOSEeaWpbE3jg39cA,_IpOC4IyjEeeQ1rr93JCGzA
Element-Name: DebugForDuplications
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _L6rZUIyzEeeQ1rr93JCGzA
Runtime-Version: 2.2.0.201706152316
Save-Time: 10/30/17 10:23 AM
Tags: Debug
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "check-thread-list"  [val all_items]{
	$all_items | each [val thread] [val index]{
		with [get-view Debug | get-tree] {
		    get-item ".* \\[Robot\\]/<suspended>Robot Test at 127.0.0.1:\d{1,5}/Tests execution thread" 
		        | get-property [format "getItems().TreeItem[%s].getText()" $index] | equals  $thread| verify-true
		}
	
	get-view Debug | get-tree 
		| get-item ".* \\[Robot\\]/<suspended>Robot Test at 127.0.0.1:\d{1,5}/Tests execution thread" 
	    	| get-property childCount | equals [$all_items | invoke getElements | invoke size]| verify-true
	}	
	        
}


//open files from first project with the same test names
//set BP on the same KW
get-view "Project Explorer" | get-tree | select "TheSameFileName/a1/s1.robot" | double-click
get-editor "s1.robot" | get-left-ruler | get-ruler-column AnnotationColumn | double-click-ruler 7

get-view "Project Explorer" | get-tree | select "TheSameFileName/a2/s1.robot" | double-click
get-editor "s1.robot" -index 1 | get-left-ruler | get-ruler-column AnnotationColumn | double-click-ruler 7

get-view "Project Explorer" | get-tree | select "TheSameFileName/a3/s1.robot" | double-click
get-editor "s1.robot" -index 2 | get-left-ruler | get-ruler-column AnnotationColumn | double-click-ruler 6

//close all editors
get-editor "s1.robot" | close
get-editor "s1.robot" | close
get-editor "s1.robot" | close

exec-with-options -disableJobWaiting -command{
	//run entire project
	get-view "Project Explorer" | get-tree | select TheSameFileName | get-menu "Debug As/1 Robot Test" | click
	wait-for-thread -file "TheSameFileName"
	
	//file from a1 folder is run
	check-debug-marker-on-ruler -line 7 -script_name "s1.robot"
	get-editor "s1.robot" | get-text-viewer | get-property text | equals "*** Test Cases ***\n"
    + "test 1\n"
    + "    Local Kw\n\n"
    + "*** Keywords ***\n"
    + "Local Kw\n"
    + "    Log  This is from a1" | verify-true
    
	check-thread-list [list "Local Kw [line: 7]"   "[Test] test 1 [line: 3]"  "[Suite] S1"  "[Suite] A1"  "[Suite] TheSameFileName" ]
	
	
	
	//------------------------ 2. file from a2 folder --------------------------
	get-button "Step Into (F5)" | click
	wait 1000
	get-button "Step Into (F5)" | click
	wait-for-specified_line-in-first-thread -line 6 -nr_of_frames 5
	
	check-debug-marker-on-ruler  -line 6 -script_name "s1.robot"
	
	get-editor "s1.robot" | get-text-viewer | get-property text | equals "*** Test Cases ***\n"
	    + "test 1\n"
	    + "    Local Kw\n\n"
	    + "*** Keywords ***\n"
	    + "Local Kw\n"
	    + "    Log  This is from a2" | verify-true
	    
	check-thread-list [list "Local Kw [line: 6]"   "[Test] test 1 [line: 3]"  "[Suite] S1"  "[Suite] A2"  "[Suite] TheSameFileName" ]
	
	// ------------------------ 3.rd file ------------------------------
	
	get-button "Resume (F8)" | click
	get-button "Resume (F8)" | click
	
	wait-for-specified_line-in-first-thread -line 6 -nr_of_frames 4
	
	check-debug-marker-on-ruler  -line 6 -script_name "s1.robot"
	
	get-editor "s1.robot" | get-text-viewer | get-property text | equals "*** Settings ***\n"
    + "Resource    res.robot\n\n"
    + "*** Test Cases ***\n"
    + "totally different case\n"
    + "    Local Kw" | verify-true
    
    check-thread-list [list "[Test] totally different case [line: 6]"   "[Suite] S1"  "[Suite] A3"  "[Suite] TheSameFileName" ]
    
    // go to resource
    get-button "Step Into (F5)" | click
    wait-for-specified_line-in-first-thread -line 2 -nr_of_frames 5
	
	check-debug-marker-on-ruler  -line 2 -script_name "res.robot"
	
    get-editor "res.robot" | get-text-viewer | get-property text | equals "*** Keywords ***\n"
    + "Local Kw\n"
    + "    Log  This is from resource" | verify-true
    
    check-thread-list [list "res.Local Kw [line: 2]"  "[Test] totally different case [line: 6]"   "[Suite] S1"  "[Suite] A3"  "[Suite] TheSameFileName" ]
    get-button "Resume (F8)" | click
  }  
	
//remove all BP	
get-view Breakpoints | get-button "Remove All Breakpoints" | click
get-window "Remove All Breakpoints" | get-button Yes | click

//set BP in second file
get-button Robot | click
get-view "Project Explorer" | get-tree | select "TheSameFileName/a2/s1.robot" | double-click
with [get-editor "s1.robot" | get-left-ruler | get-ruler-column AnnotationColumn] {
    double-click-ruler 7
}
//run this test
exec-with-options -disableJobWaiting -command{
	get-view "Project Explorer" | get-tree | select "TheSameFileName/a2/s1.robot" | get-menu "Debug As/1 Robot Test" | click
	wait-for-specified_line-in-first-thread -line 7 -nr_of_frames 5
	
	check-debug-marker-on-ruler  -line 7 -script_name "s1.robot"
	
	//check if correct file was open
	get-editor "s1.robot" | get-text-viewer | get-property text | equals "*** Test Cases ***\n"
    + "test 1\n"
    + "    Local Kw\n\n"
    + "*** Keywords ***\n"
    + "Local Kw\n"
    + "    Log  This is from a2" | verify-true
    
     check-thread-list [list "Local Kw [line: 7]"  "[Test] test 1 [line: 3]"   "[Suite] S1"  "[Suite] A2"  "[Suite] TheSameFileName" ]
	
	get-button "Resume (F8)" | click
	

}	

//run test with the same name but no BP
get-button Robot | click
get-view "Project Explorer" | get-tree | select "TheSameFileName/a1/s1.robot" | get-menu "Debug As/1 Robot Test" | click

wait-for-message-log
//check that test was run and not stopped
get-view "Message Log" | get-editbox | get-property text | equals "Starting test: TheSameFileName.A1.S1.test 1\n"
    + ".* : INFO : This is from a1\n"
    + "Ending test: TheSameFileName.A1.S1.test 1" | verify-true
	
	  
//---------------------------------------------------------------------------------

//add 2 BP on KW for in 2 tests
get-button Robot | click
get-view "Project Explorer" | get-tree | select "TwoTheSameKeywords/t1.robot" | double-click
get-editor "t1.robot" | get-left-ruler | get-ruler-column AnnotationColumn | double-click-ruler 7

get-view "Project Explorer" | get-tree | select "TwoTheSameKeywords/t2.robot" | double-click
get-editor "t2.robot" | get-left-ruler | get-ruler-column AnnotationColumn | double-click-ruler 7

//run debug on project
exec-with-options -disableJobWaiting -command{
	get-view "Project Explorer" | get-tree | select TwoTheSameKeywords | get-menu "Debug As/1 Robot Test" | click	  
	wait-for-specified_line-in-first-thread -line 7 -nr_of_frames 4
	
	
	//firat t1 is stopped
	check-debug-marker-on-ruler  -line 7 -script_name "t1.robot"
	check-thread-list [list "Local Kw [line: 7]"  "[Test] test 1 [line: 3]"  "[Suite] T1"  "[Suite] TwoTheSameKeywords" ]

	get-button "Step Into (F5)" | click  
	wait 1000  
	get-button "Step Into (F5)" | click 
	
	
	//no seond file is open
	wait-for-specified_line-in-first-thread -line 6 -nr_of_frames 4
	check-debug-marker-on-ruler  -line 6 -script_name "t2.robot"  
	
	check-thread-list [list "Local Kw [line: 6]"  "[Test] test 2 [line: 3]"   "[Suite] T2"  "[Suite] TwoTheSameKeywords" ]
	get-button "Resume (F8)" | click  
	wait 500
	get-button "Resume (F8)" | click  

}  
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
